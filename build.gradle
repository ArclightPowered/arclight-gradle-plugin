plugins {
    id 'groovy'
    id 'maven-publish'
}

group 'io.izzel.arclight'
version '1.1'

repositories {
    mavenCentral()
    maven {
        name = 'Forge Maven'
        url = 'https://files.minecraftforge.net/maven'
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    embed group: 'net.md-5', name: 'SpecialSource', version: '1.8.6'
}

jar {
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) })
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/izzelaliz/arclight-gradle-plugin")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
